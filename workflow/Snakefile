import pandas as pd
import os
import sys

p = os.path.dirname(os.getcwd())
sys.path.append(p)

from utils import *

include: 'download.smk'
configfile: 'workflow/config.yml'

# ccre stuff
ccre_df = pd.read_csv('workflow/ccre_config.tsv', sep='\t')
ccre_types = ccre_df.ccre_type.unique().tolist()
species = ccre_df.species.unique().tolist()

# procap stuff
procap_meta = process_encode_metadata('workflow/procap_metadata.tsv')

wildcard_constraints:
    pc_biosamp='|'.join([re.escape(x) for x in procap_meta.biosamp.tolist()]),
    pc_rep='|'.join([re.escape(x) for x in procap_meta.biorep.tolist()]),
    pc_output='|'.join([re.escape(x) for x in procap_meta.output.tolist()]),

rule all:
    input:
        expand(config['ccre']['bed'],
               species=species,
               ccre_type=ccre_types),
        expand(config['procap']['bed'],
               zip,
               pc_biosamp=procap_meta.biosamp.tolist(),
               pc_rep=procap_meta.biorep.tolist(),
               pc_output=procap_meta.output.tolist())

################################################################################
########################### cCRE download ######################################
################################################################################
def get_ccre_link(wc, ccre_df):
    temp = ccre_df.copy(deep=True)
    return ccre_df.loc[(ccre_df.species==wc.species)&\
                       (ccre_df.ccre_type==wc.ccre_type), 'link'].values[0]

use rule dl as dl_ccre with:
    params:
        link = lambda wc:get_ccre_link(wc, ccre_df)
    output:
        out = temporary(config['ccre']['bed_gz'])

use rule gunzip as gunzip_ccre with:
    input:
        gz = config['ccre']['bed_gz']
    output:
        out = config['ccre']['bed']

################################################################################
########################## ProCap download #####################################
################################################################################
def get_procap_encid(wc, df):
    output_type = wc.pc_output
    biosamp = wc.pc_biosamp
    rep = int(wc.pc_rep)
    temp = df.loc[(df.biosamp==biosamp)&\
                  (df.biorep==rep)&\
                  (df['output']==output_type)]
    return temp['File accession'].values[0]

use rule dl_encid_bed as dl_encid_procap_bed with:
    params:
        encid = lambda wc:get_procap_encid(wc, procap_meta)
    output:
        bed = config['procap']['bed_gz']

use rule gunzip as gunzip_procap with:
    input:
        gz = config['procap']['bed_gz']
    output:
        out = config['procap']['bed']
